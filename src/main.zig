const std = @import("std");
const is_cooking = @import("builtin").mode == .Debug;
const builtin = @import("builtin");
const DataStore = @import("DataStore.zig");
const versions_utils = @import("versions.zig");
const file_generation = @import("file_generation.zig");

// written under 0.16.0-dev.1326+2e6f7d36b

// logging stuff
pub const std_options: std.Options = if (builtin.mode == .ReleaseFast) .{
    .log_scope_levels = &.{.{ .scope = .default, .level = .info }},
} else .{};

// we are not going to test all versions of zig, so let's define the oldest one
const OLDEST_ZIG_VERSION_INCL = "0.13.0";

const SNIPPETS_DIR_NAME = "snippets";

const TMP_OUT_DIR_NAME = "tmp-out";

fn eolSeparator(comptime size: comptime_int) [size]u8 {
    return @splat('-');
}

fn getAllSnippetsPaths(gpa: std.mem.Allocator) !std.ArrayList([]const u8) {
    var paths: std.ArrayList([]const u8) = .empty;
    errdefer paths.deinit(gpa);

    var dir = try std.fs.cwd().openDir(SNIPPETS_DIR_NAME, .{ .iterate = true });
    defer dir.close();

    var walker = try dir.walk(gpa);
    defer walker.deinit();
    while (try walker.next()) |entry| {
        if (entry.kind != .file) continue;

        // making sure we're dealing with a zig file!
        if (entry.path.len < 5 or !std.mem.eql(u8, ".zig", entry.path[entry.path.len - 4 ..]))
            continue;

        const path = try gpa.dupe(u8, entry.path);
        errdefer gpa.free(path);
        try paths.append(gpa, path);

        // WARN: REMOVE THIS
        //if (is_cooking) break; // JUST GET THE FIRST PATH AND WE'RE OUT
    }

    return paths;
}

fn freeSnippetsPath(gpa: std.mem.Allocator, paths: *std.ArrayList([]const u8)) void {
    for (paths.items) |path| {
        gpa.free(path);
    }
    paths.deinit(gpa);
}

fn stringLessThan(_: void, lhs: []const u8, rhs: []const u8) bool {
    return std.ascii.orderIgnoreCase(lhs, rhs).compare(.lt);
}

inline fn assert(ok: bool, message: []const u8) void {
    if (!ok) {
        @panic(message);
    }
}

pub fn main() !void {

    // 2? allocators
    var arena_allocator = std.heap.ArenaAllocator.init(std.heap.page_allocator);
    defer arena_allocator.deinit(); // delete this? let OS clean it at the end?
    const arena = arena_allocator.allocator();

    var debug_allocator: std.heap.DebugAllocator(.{}) = .init;
    const gpa, const is_debug = gpa: {
        break :gpa switch (builtin.mode) {
            .Debug, .ReleaseSafe => .{ debug_allocator.allocator(), true },
            .ReleaseFast, .ReleaseSmall => .{ std.heap.smp_allocator, false },
        };
    };
    defer if (is_debug) {
        _ = debug_allocator.deinit();
    };

    var threaded: std.Io.Threaded = .init(gpa);
    defer threaded.deinit();
    const io = threaded.io();

    std.log.info("starting ZNIPPETS {s}", .{eolSeparator(80 - 18)});

    // 0. Testing that zigup exists -------------------------------------------
    std.log.debug("0. Testing zigup {s}", .{eolSeparator(80 - 17)});
    var zigup_existence_proc = std.process.Child.init(
        &.{ "sh", "-c", "command -v zigup" },
        gpa,
    );
    zigup_existence_proc.stdout_behavior = .Ignore;
    const zigup_test_existence_res = zigup_existence_proc.spawnAndWait() catch |err| {
        std.log.err("{}", .{err});
        @panic("Checking the existence of zigup failed");
    };
    if (zigup_test_existence_res.Exited == 0) {
        std.log.debug("0.1 zigup was found. Proceeding...", .{});
    } else {
        std.log.err(
            \\1.1 zigup was NOT found. \n
            \\    Install it or make sure it exists
            \\    Exiting!
            \\
        , .{});
        std.process.exit(1);
    }

    // 1. Get the versions
    std.log.debug("1. Acquiring the Zig versions {s}", .{eolSeparator(80 - 30)});
    std.log.debug("1.1 Reading VERSIONS file (if it exists)", .{});
    var zig_versions = try DataStore.getVersions(arena, io);
    // versions are sorted from oldest to newest, last line = master
    // all old snippets have already been tested with zig_versions[0..new_version_idx]
    // new_version_idx correspond to the idx of the first version that needs to test all snippets!
    var new_version_idx = zig_versions.items.len;

    std.log.debug("1.2 Looking for new available versions", .{});
    // let's fetch all existing versions and add the newest one!
    // If there's a new master (dev) version, then it replaces the existing one
    // If there's a new release it's added to the list too
    // all versions are sorted newest first
    const all_versions = ver_blk: {
        if (is_debug) {
            std.log.debug("USING STUB", .{});
            break :ver_blk try versions_utils.fetchZigVersionsStub(gpa, arena, io);
        } else {
            break :ver_blk try versions_utils.fetchZigVersions(gpa, arena, io); // could catch error and continue
        }
    };
    assert(all_versions.items.len != 0, "No version was fetched remotely, that's not normally!");
    // make your life easier by having oldest first, like zig_versions
    // it's easier to store the tests' results that way
    // (it's not that it's hard, but I don't really want to change anything right meow)
    std.mem.reverse([]const u8, all_versions.items);

    var previousMaster: ?[]const u8 = null;

    if (zig_versions.items.len == 0) {
        std.log.debug("zig_versions empty", .{});
        // so VERSIONS file is empty
        // need to fill it with all_versions starting with OLDEST_ZIG_VERSION_INCL
        // INFO: OLDEST_ZIG_VERSION_INCL could become a (CLI) argument!

        var starting_idx: usize = 0;
        for (all_versions.items) |ver| {
            if (std.mem.eql(u8, OLDEST_ZIG_VERSION_INCL, ver)) {
                break;
            }
            starting_idx += 1;
        }
        // empty file + no matching version? really?
        assert(starting_idx < all_versions.items.len, "VERSIONS file empty and couldn't find OLDEST_ZIG_VERSION_INCL");

        try zig_versions.appendSlice(arena, all_versions.items[starting_idx..]);

        // otherwise, if not empty, we assume a previous normal run that filled VERSIONS
        // so last element should be the master branch!
    } else if (zig_versions.items.len == 1) {
        @panic(
            \\Only one line was found in the VERSIONS file.
            \\This file should contain 0 or at least 2 versions: a stable one and a master (dev)!
        );
    } else if (!versions_utils.areMasterVersionsTheSame(&zig_versions, &all_versions)) {
        // if masters are different there is at least one new version to test
        // what if there's a new release but not a new master, would that be possible?
        std.log.debug("DEBUG zig_versions master not matching local master", .{});
        previousMaster = zig_versions.pop().?; // pop old master
        new_version_idx -= 1;
        var i: usize = 1;
        // search for the last (newest) corresponding version
        while (!std.mem.eql(u8, zig_versions.items[zig_versions.items.len - 1], all_versions.items[all_versions.items.len - 1 - i])) {
            i += 1;
        }
        const starting_idx = all_versions.items.len - i;
        try zig_versions.appendSlice(arena, all_versions.items[starting_idx..]);
    }
    // else: masters are matching
    // WARN: assume there cannot be a new release if no new master,
    // should be a pretty safe assumption!

    std.log.debug("1.3 Printing the versions to test", .{});
    for (zig_versions.items) |ver| {
        std.log.debug("{s}", .{ver});
    }

    // 2. Get list of snippets ------------------------------------------------
    std.log.debug("2. Getting all snippets {s}", .{eolSeparator(80 - 24)});
    std.log.debug("2.1 Fetching OLD snippets and results from SNIPPETS file", .{});
    var snippets_paths, var tests_results = try DataStore.getSnippets(arena, io);
    std.log.debug("    {d} snippets found.", .{snippets_paths.items.len});

    std.log.debug("2.2.1 Searching for snippets inside {s} directory", .{SNIPPETS_DIR_NAME});
    var snippets_paths_local = try getAllSnippetsPaths(gpa);
    std.log.debug("      {d} snippets found.", .{snippets_paths_local.items.len});
    // 2.1 unstable sort
    std.log.debug("2.2.2 Sorting paths by alpha order", .{});
    std.sort.pdq([]const u8, snippets_paths_local.items, {}, stringLessThan);

    std.log.debug("2.3 Merging OLD and NEW snippets", .{});
    const old_len = snippets_paths.items.len;
    const new_len = snippets_paths_local.items.len;
    if (old_len > new_len) {
        // I don't want to think about deletion for now!
        // If you want to delete a snippet, just delete it and the corresponding line
        @panic(
            \\ SNIPPETS files contains more snippets than the number of actual snippets
            \\ If a snippet was deleted, please delete the corresponding line in SNIPPETS
            \\ If some other bug, delete the SNIPPETS info file and retest the whole thing
        );
    } else if (old_len < new_len) {
        // we have 2 sorted lists, we compare each list, if a new snippet is spotted,
        // we put it at the end of the "old" list
        var old_idx: usize = 0;
        var new_idx: usize = 0;
        while (old_idx < old_len) : (new_idx += 1) {
            if (!std.mem.eql(u8, snippets_paths.items[old_idx], snippets_paths_local.items[new_idx])) {
                const new_snip = try std.mem.Allocator.dupe(arena, u8, snippets_paths_local.items[new_idx]);
                try snippets_paths.append(arena, new_snip);
                std.log.debug("    New snippet: {s}", .{new_snip});
                continue;
            }
            old_idx += 1;
        }
        // let's not forget to add any remaining new snippet
        while (new_idx < new_len) : (new_idx += 1) {
            const new_snip = try std.mem.Allocator.dupe(arena, u8, snippets_paths_local.items[new_idx]);
            try snippets_paths.append(arena, new_snip);
            std.log.debug("    New snippet: {s}", .{new_snip});
        }
        try tests_results.appendNTimes(arena, 0, new_len - old_len);
    } else { // same number of snippet
        // what if I delete a snippet and add a new one?
        // [NO.](https://www.youtube.com/watch?v=qQrvoDzzLfk)
        // don't do that, the file should be updated, if you delete a file
        // you delete the line, otherwise this is going to be a disaster
        //
        // pfft, this is not very robust, you didn't think about that didn't you?!
        // well let's keep going and assume we don't delete files! why would we
        // in the first place!

        // no new version
        if (new_version_idx == zig_versions.items.len) {
            std.log.info("[END] Nothing new, exiting early", .{});
            freeSnippetsPath(gpa, &snippets_paths_local);
            return;
        }
    }
    // we can list the (new) local list, we don't need it anymore!
    freeSnippetsPath(gpa, &snippets_paths_local);

    // 3. Let's test the tests ------------------------------------------------
    std.log.debug("3. Let's test our snippets {s}", .{eolSeparator(80 - 27)});
    // var tests_results: std.ArrayList(u64) = .empty;
    // try tests_results.appendNTimes(arena, 0, snippets_paths.items.len);

    std.log.debug("3.1 Running the tests", .{});
    for (zig_versions.items, 0..) |version_name, version_idx| {
        std.log.debug("3.1.{d} Testing version {s}", .{ version_idx + 1, version_name });

        var zigup_process = std.process.Child.init(&.{ "zigup", version_name }, gpa);
        zigup_process.stderr_behavior = .Ignore;
        const zigup_term = try zigup_process.spawnAndWait();
        switch (zigup_term) {
            .Exited => |exit_code| {
                if (exit_code != 0) {
                    std.log.err("Something went wrong with zigup (1)", .{});
                    continue;
                }
            },
            else => std.log.err("Something went wrong with zigup (2)", .{}),
        }

        // So, here we don't need to tests the OLD snippets (i.e. the "old_len"
        // first snippets of the snippets_paths array) for the `new_version_idx`
        // first versions!
        const starting_idx = if (version_idx < new_version_idx) old_len else 0;
        var processes: std.ArrayList(std.process.Child) = .empty;
        defer processes.deinit(gpa);
        for (snippets_paths.items[starting_idx..], 0..) |path, proc_idx| {
            std.log.debug("{d} ", .{proc_idx});
            std.log.debug("{s}", .{path});
            const snip_path = try std.fmt.allocPrint(gpa, "{s}/{s}", .{ SNIPPETS_DIR_NAME, path });
            defer gpa.free(snip_path);
            try processes.append(
                gpa,
                std.process.Child.init(&.{ "zig", "test", snip_path }, gpa),
            );
            processes.items[proc_idx].stdout_behavior = .Ignore;
            processes.items[proc_idx].stderr_behavior = .Ignore;
            try processes.items[proc_idx].spawn();
        }
        for (processes.items, 0..) |*process, proc_idx| {
            const term: std.process.Child.Term = try process.wait();
            var failed = true;
            switch (term) {
                .Exited => |exit_code| {
                    if (exit_code == 0) {
                        tests_results.items[proc_idx + starting_idx] |= @as(u64, 1) << @intCast(version_idx);
                        failed = false;
                    }
                },
                else => {
                    // idk what happens here
                    std.log.err("Unexpected behavior: Term was not .Exited ", .{});
                },
            }
        }
    }

    if (is_debug) {
        std.log.debug("4. Reporting results {s}", .{eolSeparator(80 - 21)});
        for (snippets_paths.items, 0..) |path, snippet_idx| {
            std.log.debug("4.{d} {s}", .{ snippet_idx, path });
            const res = tests_results.items[snippet_idx];
            std.log.debug("    Success: ", .{});
            for (zig_versions.items, 0..) |version_name, version_idx| {
                if (res & @as(u64, 1) << @intCast(version_idx) != 0) {
                    std.log.debug("{s} ", .{version_name});
                }
            }
            std.log.debug("\n    Failure: ", .{});
            for (zig_versions.items, 0..) |version_name, version_idx| {
                if (res & @as(u64, 1) << @intCast(version_idx) == 0) {
                    std.log.debug("{s} ", .{version_name});
                }
            }
            std.log.debug("", .{});
        }
    }

    std.log.debug("5. Saving results and versions in files! {s}", .{eolSeparator(80 - 41)});
    std.log.debug("5.1 Sorting paths by alpha order and results", .{});
    // 5.1 unstable sort
    // std.sort.pdq([]const u8, snippets_paths.items, {}, stringLessThan);
    // ^^^^^ pfft I forgot I need to sort the results as well
    // how did I not think about that? [am I stupid?](https://tenor.com/Xjyl.gif)
    sortPathAndResBecauseImStupidAndMultiArraylistWouldProbablyBeBetterButIjustWantToGetSmthWorkingNow(&snippets_paths, &tests_results);
    std.log.debug("5.2 Saving snippets and results", .{});
    for (tests_results.items) |res| {
        std.log.debug("res: {d}", .{res});
    }
    try DataStore.saveSnippetsAndResults(&snippets_paths, &tests_results);

    try DataStore.saveVersions(&zig_versions);

    // File generation --------------------------------------------------------
    std.log.debug("6. Jenna raiding html files for each snippets {s}", .{eolSeparator(80 - 46)});
    std.log.debug("6.1 Opening template.html", .{});

    // let's store the filenames to be able to reuse them
    var html_filenames = FilenameList.init;

    var template_buf: [4096]u8 = undefined;
    const template_file = try std.Io.Dir.cwd().openFile(io, "html-templates/template.html", .{ .mode = .read_only });
    defer template_file.close(io);
    var template_reader = template_file.reader(io, &template_buf);

    // TODO: AWKWARD MIX of Io and old stuff
    // once https://github.com/ziglang/zig/issues/25738 is done, move to Io

    // create new folder for files creation
    std.log.debug("6.2 Creating temporary output directory", .{});
    // first deleting it if it exists, probable cause:
    // previous execution failed, need to clean up the mess
    try std.fs.cwd().deleteTree(TMP_OUT_DIR_NAME);
    const tmp_out_dir = try std.fs.cwd().makeOpenPath(TMP_OUT_DIR_NAME, .{});

    for (snippets_paths.items, 0..) |path, snippet_idx| {
        // TODO: alright maybe be a bit careful here about the path names!
        // especially about sperators in the path...
        const new_filename = try html_filenames.allocPrintAppend(arena, "{s}.html", .{path[0 .. path.len - 4]});
        const html_file = try tmp_out_dir.createFile(new_filename, .{});
        defer html_file.close();
        var out_buf: [4096]u8 = undefined;
        var out_writer = html_file.writer(&out_buf);

        const res = tests_results.items[snippet_idx];

        while (file_generation.streamUntilTemplateStr(&template_reader.interface, &out_writer.interface)) |template_name| {
            if (std.mem.eql(u8, "TITLE", template_name)) {
                try out_writer.interface.print("{s}", .{path});
            } else if (std.mem.eql(u8, "WORKING_VERSIONS", template_name)) {
                for (zig_versions.items, 0..) |version_name, version_idx| {
                    if (res & @as(u64, 1) << @intCast(version_idx) != 0) {
                        try out_writer.interface.print("<a class=\"version-link\" href=\"v{s}.html\">{s}</a> ", .{ version_name, version_name });
                    }
                }
            } else if (std.mem.eql(u8, "FAILING_VERSIONS", template_name)) {
                for (zig_versions.items, 0..) |version_name, version_idx| {
                    if (res & @as(u64, 1) << @intCast(version_idx) == 0) {
                        try out_writer.interface.print("<a class=\"version-link\" href=\"v{s}.html\">{s}</a> ", .{ version_name, version_name });
                    }
                }
            } else if (std.mem.eql(u8, "CODE", template_name)) {
                const snip_path = try std.fmt.allocPrint(gpa, "{s}/{s}", .{ SNIPPETS_DIR_NAME, path });
                defer gpa.free(snip_path);
                var code_buf: [4096]u8 = undefined;
                const code_file = try std.Io.Dir.cwd().openFile(io, snip_path, .{ .mode = .read_only });
                defer code_file.close(io);
                var code_reader = code_file.reader(io, &code_buf);
                _ = try out_writer.interface.sendFileAll(&code_reader, .unlimited);
            }
        } else |err| switch (err) {
            error.EndOfStream => {},
            else => std.log.err("\n An error occured, while creating the {s} file: {}", .{ new_filename, err }),
        }
        try template_reader.seekTo(0);
    }

    std.log.debug("7. Jenna raiding html files for each version {s}", .{eolSeparator(80 - 45)});
    std.log.debug("7.1 Opening version-template.html", .{});

    const v_template_file = try std.Io.Dir.cwd().openFile(io, "html-templates/version-template.html", .{ .mode = .read_only });
    defer v_template_file.close(io);
    // let's reuse the same buffer!
    var v_template_reader = v_template_file.reader(io, &template_buf);
    const snippet_html_file_total_count = html_filenames.list.items.len;
    for (zig_versions.items, 0..) |version, version_idx| {
        const new_filename = try html_filenames.allocPrintAppend(arena, "v{s}.html", .{version});
        const html_file = try tmp_out_dir.createFile(new_filename, .{});
        defer html_file.close();
        var out_buf: [4096]u8 = undefined;
        var out_writer = html_file.writer(&out_buf);

        while (file_generation.streamUntilTemplateStr(&v_template_reader.interface, &out_writer.interface)) |template_str| {
            if (std.mem.eql(u8, "VERSION", template_str)) {
                try out_writer.interface.print("{s}", .{version});
            } else if (std.mem.eql(u8, "WORKING_SNIPPETS", template_str)) {
                for (tests_results.items, 0..) |res, snippet_idx| {
                    if (res & @as(u64, 1) << @intCast(version_idx) != 0) {
                        const snippet_html_file = html_filenames.at(snippet_idx);
                        const snippet_name = snippets_paths.items[snippet_idx];
                        try out_writer.interface.print("<a href=\"{s}\">{s}</a><br>", .{ snippet_html_file, snippet_name });
                    }
                }
            } else if (std.mem.eql(u8, "FAILING_SNIPPETS", template_str)) {
                for (tests_results.items, 0..) |res, snippet_idx| {
                    if (res & @as(u64, 1) << @intCast(version_idx) == 0) {
                        const snippet_html_file = html_filenames.at(snippet_idx);
                        const snippet_name = snippets_paths.items[snippet_idx];
                        try out_writer.interface.print("<a href=\"{s}\">{s}</a><br>", .{ snippet_html_file, snippet_name });
                    }
                }
            }
        } else |err| switch (err) {
            error.EndOfStream => {},
            else => std.log.err("\n An error occured, while creating the {s} file: {}", .{ new_filename, err }),
        }
        try out_writer.interface.flush();
        try v_template_reader.seekTo(0);
    }

    //-------------------------------------------------------------------------
    // PREV_VERSION, VERSION, STILL_FAILING_SNIPPETS, STILL_WORKING_SNIPPETS, NEW_FAILING_SNIPPETS, NEW_WORKING_SNIPPETS
    std.log.debug("8. Generating version changes html files {s}", .{eolSeparator(80 - 41)});
    const changes_template_file = try std.Io.Dir.cwd().openFile(io, "html-templates/changes-template.html", .{ .mode = .read_only });
    defer changes_template_file.close(io);
    var changes_template_reader = changes_template_file.reader(io, &template_buf);
    var changes_idx: usize = 1;
    while (changes_idx < zig_versions.items.len) : (changes_idx += 1) {
        const filename = try html_filenames.allocPrintAppend(arena, "{s}_to_{s}.html", .{ zig_versions.items[changes_idx - 1], zig_versions.items[changes_idx] });
        const changes_file = try tmp_out_dir.createFile(filename, .{});
        defer changes_file.close();
        var chg_buf: [4096]u8 = undefined;
        var chg_writer = changes_file.writer(&chg_buf);

        while (file_generation.streamUntilTemplateStr(&changes_template_reader.interface, &chg_writer.interface)) |template_str| {
            if (std.mem.eql(u8, "VERSION", template_str)) {
                try chg_writer.interface.print("{s}", .{zig_versions.items[changes_idx]});
            } else if (std.mem.eql(u8, "PREV_VERSION", template_str)) {
                try chg_writer.interface.print("{s}", .{zig_versions.items[changes_idx - 1]});
            } else if (std.mem.eql(u8, "STILL_FAILING_SNIPPETS", template_str)) {
                for (tests_results.items, 0..) |res, snippet_idx| {
                    if ((res & @as(u64, 1) << @as(u6, @intCast(changes_idx))) == 0 and (res & @as(u64, 1) << @as(u6, @intCast(changes_idx)) - 1) == 0) {
                        const snippet_html_file = html_filenames.at(snippet_idx);
                        const snippet_name = snippets_paths.items[snippet_idx];
                        try chg_writer.interface.print("<a href=\"{s}\">{s}</a><br>", .{ snippet_html_file, snippet_name });
                    }
                }
            } else if (std.mem.eql(u8, "STILL_WORKING_SNIPPETS", template_str)) {
                for (tests_results.items, 0..) |res, snippet_idx| {
                    if ((res & @as(u64, 1) << @as(u6, @intCast(changes_idx))) != 0 and (res & @as(u64, 1) << @as(u6, @intCast(changes_idx)) - 1) != 0) {
                        const snippet_html_file = html_filenames.at(snippet_idx);
                        const snippet_name = snippets_paths.items[snippet_idx];
                        try chg_writer.interface.print("<a href=\"{s}\">{s}</a><br>", .{ snippet_html_file, snippet_name });
                    }
                }
            } else if (std.mem.eql(u8, "NEW_FAILING_SNIPPETS", template_str)) {
                for (tests_results.items, 0..) |res, snippet_idx| {
                    if ((res & @as(u64, 1) << @as(u6, @intCast(changes_idx))) == 0 and (res & @as(u64, 1) << @as(u6, @intCast(changes_idx)) - 1) != 0) {
                        const snippet_html_file = html_filenames.at(snippet_idx);
                        const snippet_name = snippets_paths.items[snippet_idx];
                        try chg_writer.interface.print("<a href=\"{s}\">{s}</a><br>", .{ snippet_html_file, snippet_name });
                    }
                }
            } else if (std.mem.eql(u8, "NEW_WORKING_SNIPPETS", template_str)) {
                for (tests_results.items, 0..) |res, snippet_idx| {
                    if ((res & @as(u64, 1) << @as(u6, @intCast(changes_idx))) != 0 and (res & @as(u64, 1) << @as(u6, @intCast(changes_idx)) - 1) == 0) {
                        const snippet_html_file = html_filenames.at(snippet_idx);
                        const snippet_name = snippets_paths.items[snippet_idx];
                        try chg_writer.interface.print("<a href=\"{s}\">{s}</a><br>", .{ snippet_html_file, snippet_name });
                    }
                }
            }
        } else |err| switch (err) {
            error.EndOfStream => {},
            else => std.log.err("\n An error occured, while creating the index.html file: {}", .{err}),
        }
        try changes_template_reader.seekTo(0);
        try chg_writer.interface.flush();
    }

    std.log.debug("9. Jenna raiding index.html {s}", .{eolSeparator(80 - 28)});

    const index_template_file = try std.Io.Dir.cwd().openFile(io, "html-templates/index-template.html", .{ .mode = .read_only });
    defer index_template_file.close(io);
    // let's reuse the same buffer!
    var index_template_reader = index_template_file.reader(io, &template_buf);

    const index_html = try tmp_out_dir.createFile("index.html", .{});
    defer index_html.close();
    var out_buf: [4096]u8 = undefined;
    var out_writer = index_html.writer(&out_buf);
    const last_version_file = snippet_html_file_total_count + zig_versions.items.len;
    while (file_generation.streamUntilTemplateStr(&index_template_reader.interface, &out_writer.interface)) |template_str| {
        if (std.mem.eql(u8, "SNIPPETS", template_str)) {
            for (0..snippet_html_file_total_count) |snippet_idx| {
                const snippet_html_file = html_filenames.at(snippet_idx);
                const snippet_name = snippets_paths.items[snippet_idx];
                try out_writer.interface.print("<a href=\"{s}\">{s}</a><br>", .{ snippet_html_file, snippet_name });
            }
        } else if (std.mem.eql(u8, "VERSIONS", template_str)) {
            for (snippet_html_file_total_count..last_version_file, zig_versions.items) |idx, version_name| {
                const version_html_filename = html_filenames.at(idx);
                try out_writer.interface.print("<a href=\"{s}\">{s}</a><br>", .{ version_html_filename, version_name });
            }
        } else if (std.mem.eql(u8, "CHANGES", template_str)) {
            for (last_version_file..html_filenames.list.items.len) |idx| {
                const version_html_filename = html_filenames.at(idx);
                try out_writer.interface.print("<a href=\"{s}\">{s}</a><br>", .{ version_html_filename, version_html_filename });
            }
        } else if (std.mem.eql(u8, "FOOTER", template_str)) {
            file_generation.writeDateTime(io, &out_writer.interface);
        }
    } else |err| switch (err) {
        error.EndOfStream => {},
        else => std.log.err("\n An error occured, while creating the index.html file: {}", .{err}),
    }
    try out_writer.interface.flush();

    std.log.debug("10. Publishing web pages {s}", .{eolSeparator(80 - 26)});
    std.log.debug("10.1 Delete docs/", .{});
    try std.fs.cwd().deleteTree("docs");
    std.log.debug("10.2 Rename tmp-out to docs/", .{});
    try std.fs.cwd().rename("tmp-out", "docs");
    std.log.debug("10.3 Copy style.css", .{});
    try std.fs.Dir.copyFile(std.fs.cwd(), "html-templates/style.css", std.fs.cwd(), "docs/style.css", .{});

    std.log.debug("11. Minify html files {s}", .{eolSeparator(80 - 21)});
    file_generation.minifyGeneratedFiles(gpa, "docs/");

    // deleting previous installed master version to minimize storage
    if (previousMaster) |prevMaster| {
        std.log.debug("12. deleting previous master {s}", .{eolSeparator(80 - 29)});
        var zigup_process = std.process.Child.init(&.{ "zigup", "clean", prevMaster }, gpa);
        zigup_process.stderr_behavior = .Ignore;
        const zigup_term = try zigup_process.spawnAndWait();
        switch (zigup_term) {
            .Exited => |exit_code| {
                if (exit_code != 0) {
                    std.log.err("Something went wrong with zigup clean", .{});
                }
            },
            else => std.log.err("Something went wrong with zigup clean (2)", .{}),
        }
    }

    std.log.debug("13. commit and push {s}", .{eolSeparator(80 - 20)});
    if (!is_debug) {
        gitCommitPush(gpa);
    }

    return;
}

const FilenameList = struct {
    list: std.ArrayList([]const u8),

    const Self = @This();

    pub fn allocPrintAppend(self: *Self, arena: std.mem.Allocator, comptime fmt: []const u8, args: anytype) ![]u8 {
        var formatted = try std.fmt.allocPrint(arena, fmt, args);
        // checking for directory separator char
        for (0..formatted.len) |i| {
            if (formatted[i] == std.fs.path.sep) {
                formatted[i] = '-';
            }
        }
        try self.list.append(arena, formatted);
        return formatted;
    }

    pub fn at(self: Self, idx: usize) []const u8 {
        std.debug.assert(idx < self.list.items.len);
        return self.list.items[idx];
    }

    const init = Self{
        .list = .empty,
    };
};

fn sortPathAndResBecauseImStupidAndMultiArraylistWouldProbablyBeBetterButIjustWantToGetSmthWorkingNow(snip: *std.ArrayList([]const u8), res: *std.ArrayList(u64)) void {
    const Context = struct {
        snip: [][]const u8,
        res: []u64,
        pub fn lessThan(ctx: @This(), lhs: usize, rhs: usize) bool {
            return std.ascii.orderIgnoreCase(ctx.snip[lhs], ctx.snip[rhs]).compare(.lt);
        }

        pub fn swap(ctx: @This(), lhs: usize, rhs: usize) void {
            std.mem.swap([]const u8, &ctx.snip[lhs], &ctx.snip[rhs]);
            std.mem.swap(u64, &ctx.res[lhs], &ctx.res[rhs]);
        }
    };

    const ctx: Context = .{ .snip = snip.items, .res = res.items };
    std.sort.pdqContext(0, res.items.len, ctx);
}

fn gitCommitPush(gpa: std.mem.Allocator) void {
    var git_proc = std.process.Child.init(
        &.{ "sh", "-c", "git add SNIPPETS VERSIONS docs/ && git commit -m 'automatic push' && git push" },
        gpa,
    );
    // git_proc.stdout_behavior = .Ignore;
    const git_res = git_proc.spawnAndWait() catch |err| {
        std.log.err("git commit and push failed: {}", .{err});
        return;
    };
    if (git_res.Exited != 0) {
        std.log.err("git commit & push failed with exit code: {d}", .{git_res.Exited});
    }
}
